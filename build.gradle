plugins {
    id 'java'
}
apply plugin: 'maven'
apply plugin: 'signing'
archivesBaseName = 'retryer'
group 'io.github.anirban-a'
version '1.0.0-SNAPSHOT'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
repositories {
    mavenCentral()
}
signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Retryer'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A framework for retrying tasks'

                scm {
                    connection 'scm:https://github.com/anirban-a/retryer.git'
                    developerConnection 'scm:https://github.com/anirban-a/retryer.git'
                    url 'https://github.com/anirban-a/retryer'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'anirban-a'
                        name 'Anirban Acharya'
                        email 'acharya.anirban9@gmail.com'
                    }
                }
            }
        }
    }
}


dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
}
